****************************************************
Single Category Picture Implicit Attitude Test (SC-IAT)
****************************************************
Copyright ©  04-01-2015 Millisecond Software
modified by Ian Hussey

<values>
/showsummaryfeedback = false
/pretrialpause = 250
/ showreminder = false
/ reminderRT = 2000
/ reminderduration = 500
</values>

****************************************************
stimuli
****************************************************

<item attributeAlabel>
/1 = "Bad"
</item>

<item attributeA>
/ 1 = "Evil"
/ 2 = "Agony"
/ 3 = "Awful"
/ 4 = "Nasty"
/ 5 = "Terrible"
/ 6 = "Horrible"
/ 7 = "Failure"
/ 8 = "Hurt"
</item>

<item attributeBlabel>
/1 = "Good"
</item>

<item attributeB>
/ 1 = "Joy"
/ 2 = "Happy"
/ 3 = "Laughter"
/ 4 = "Love"
/ 5 = "Glorious"
/ 6 = "Pleasure"
/ 7 = "Peace"
/ 8 = "Wonderful"
</item>

<item targetAlabel>
/1 = "Chinese characters"
</item>

<item targetA>
/1 = "pic1.jpg"
/2 = "pic2.jpg"
/3 = "pic3.jpg"
/4 = "pic4.jpg"
/5 = "pic5.jpg"
/6 = "pic6.jpg"
</item>

****************************************************
defaults
****************************************************

<defaults>
/ fontstyle = ("Arial", 3.5%)
/ screencolor = (0,0,0)
/ txbgcolor = (0,0,0)
/ txcolor = (255, 255, 255)
/ minimumversion = "4.0.0.0"
</defaults>

****************************************************
scoring
****************************************************

<values>
/completed = 0
/correct = 0
/ sum1 = 0
/ sum2 = 0
/ n1 = 0
/ n2 = 0
/ ss1 = 0
/ ss2 = 0
/ magnitude = "unknown"
/ n_correct = 0

/key_left = 18
/key_left_label = "E"
/key_right = 23
/key_right_label = "I"
</values>

<expressions>
/ m1 = values.sum1 / values.n1
/ m2 = values.sum2 / values.n2
/ sd1 = sqrt((values.ss1 - (values.n1 * (expressions.m1 * expressions.m1))) / (values.n1 - 1))
/ sd2 = sqrt((values.ss2 - (values.n2 * (expressions.m2 * expressions.m2))) / (values.n2 - 1))
/ sd = sqrt((((values.n1 - 1) * (expressions.sd1 * expressions.sd1) + (values.n2 - 1) * (expressions.sd2 * expressions.sd2)) + ((values.n1 + values.n2) * ((expressions.m1 - expressions.m2) * (expressions.m1 - expressions.m2)) / 4) ) / (values.n1 + values.n2 - 1) )
/ d = (m2 - m1) / expressions.sd
/ latdiff = m2 - m1
/ attitude = "unknown"
/ percentcorrect = (values.n_correct/ (block.compatibletest_sciat.trialcount + block.incompatibletest_sciat.trialcount)) * 100
</expressions>

<data>
/file = "SCIAT_rawdata.iqdat"
/separatefiles = true
/ columns = [date, time, group, subject, blockcode, blocknum, trialcode, trialnum, 
			response, correct, latency, stimulusnumber, stimulusitem,
			expressions.d, expressions.latdiff, expressions.percentcorrect]
</data>

<summarydata >
/file = "SCIAT_summary.iqdat"
/columns = [script.startdate, script.starttime, script.subjectid, script.groupid, script.elapsedtime, values.completed, 
			expressions.m1, expressions.sd1, expressions.m2, expressions.sd2,
			expressions.d, expressions.latdiff, expressions.percentcorrect]
</summarydata>

<trial summary>
/ stimulustimes = [0=summary]
/ validresponse = (" ")
/ recorddata = false
/ ontrialbegin = [values.magnitude = "little to no"]
/ ontrialbegin = [if( abs(expressions.d) > 0.15 ) values.magnitude = "a slight"]
/ ontrialbegin = [if( abs(expressions.d) > 0.35 ) values.magnitude = "a moderate"]
/ ontrialbegin = [if( abs(expressions.d) >= 0.65 ) values.magnitude = "a strong"]
/ ontrialbegin = [if(expressions.d >= 0.0) expressions.attitude = "positive"]
/ ontrialbegin = [if(expressions.d < 0.0) expressions.attitude = "negative"]
</trial>

<text summary>
/ items = ("Your IAT score (D) was <% expressions.d %>, which suggests <% values.magnitude %> <% expressions.attitude %> implicit attitude towards <% item.targetALabel.1 %>.~n~n~nPress the spacebar to complete this session.") 
/ size = (60%, 60%)
/ hjustify = left
 </text>

****************************************************
instructions
****************************************************

<trial instructions>
/ stimulustimes = [1=instructions, spacebar]
/ correctresponse = (" ")
/ errormessage = false
/ recorddata = false
</trial>

<text instructions>
/ items = instructions
/ hjustify = left
/ size = (90%, 60%)
/ position = (50%, 85%)
/ valign = bottom
/ select = sequence
/ resetinterval = 20
</text>

<item instructions>
/ 1 = "
Pictures and words will appear in the middle of the screen. 

Categorize them using the E key for categories on the left and I key for categories on the right. 

If you make an error, an X will appear - hit the correct key to continue.

GO AS FAST AS YOU CAN while making as few mistakes as possible."
/ 2 = "
Notice above, there are now three cateogries to be categorized."
/ 3 = "
See above, one of the three categories has changed sides. Sort the categories using the new configuration."
</item>

<text spacebar>
/ items = ("Press the SPACE BAR to begin.")
/ position = (50%, 95%)
/ valign = bottom
</text>

<text attributeA>
/ items = attributeA
/ fontstyle = ("Arial", 5%)
/ txcolor = orange
</text>

<text attributeB>
/ items = attributeB
/ fontstyle = ("Arial", 5%)
/ txcolor = orange
</text>

<picture targetA>
/ items = targetA
/ size = (20%, 20%)
</picture>

<text error>
/ position = (50%, 75%)
/ items = ("X")
/ color = (255, 0, 0)
/ fontstyle = ("Arial", 10%, true)
</text>

<text attributeAleft>
/ items = attributeAlabel
/ valign = top
/ halign = left
/ position = (5%, 19%)
/ txcolor = orange
/ fontstyle = ("Arial", 5%)
</text>

<text attributeBright>
/ items = attributeBlabel
/ valign = top
/ halign = right
/ position = (95%, 19%)
/ txcolor = orange
/ fontstyle = ("Arial", 5%)
</text>

<text targetAleftmixed>
/ items = targetAlabel
/ valign = top
/ halign = left
/ position = (5%, 5%)
/ fontstyle = ("Arial", 5%)
</text>

<text targetArightmixed>
/ items = targetAlabel
/ valign = top
/ halign = right
/ position = (95%, 5%)
/ fontstyle = ("Arial", 5%)
</text>

<text targetAleft>
/ items = targetAlabel
/ valign = top
/ halign = left
/ position = (5%, 5%)
/ fontstyle = ("Arial", 5%)
</text>

<text targetAright>
/ items = targetAlabel
/ valign = top
/ halign = right
/ position = (95%, 5%)
/ fontstyle = ("Arial", 5%)
</text>

<text orleft>
/ items = ("or")
/ valign = top
/ halign = left
/ position = (5%, 12%)
/ fontstyle = ("Arial", 5%)
</text>

<text orright>
/ items = ("or")
/ valign = top
/ halign = right
/ position = (95%, 12%)
/ fontstyle = ("Arial", 5%)
</text>

<text reminder>
/items = ("Please, respond more quickly!")
/txcolor = yellow
/ fontstyle = ("Arial", 5%, false, false, false, false, 5, 1)
</text>

****************************************************
trials
****************************************************

<trial attributeA>
/pretrialpause = values.pretrialpause
/stimulusframes = [1 = attributeA]
/validresponse = (18, 23)
/correctresponse = (18)
/responsemessage = (23,error,150)

/ontrialend = [if (trial.attributea.correct) values.correct = 1 else values.correct = 0]
/branch = [if (values.showreminder == true && trial.attributea.latency >= values.reminderrt) trial.reminder]
</trial>

<trial attributeB>
/pretrialpause = values.pretrialpause
/stimulusframes = [1 = attributeB]
/validresponse = (18, 23)
/correctresponse = (23)
/responsemessage = (18,error,150)

/ontrialend = [if (trial.attributeb.correct) values.correct = 1 else values.correct = 0]
/branch = [if (values.showreminder == true && trial.attributeB.latency >= values.reminderrt) trial.reminder]
</trial>

<trial targetAleft>
/pretrialpause = values.pretrialpause
/stimulusframes = [1 = targetA]
/validresponse = (18, 23)
/correctresponse = (18)
/responsemessage = (23,error,150)

/ontrialend = [if (trial.targetAleft.correct) values.correct = 1 else values.correct = 0]
/branch = [if (values.showreminder == true && trial.targetAleft.latency >= values.reminderrt) trial.reminder]
</trial>

<trial targetAright>
/pretrialpause = values.pretrialpause
/stimulusframes = [1 = targetA]
/validresponse = (18, 23)
/correctresponse = (23)
/responsemessage = (18,error,150)

/ontrialend = [if (trial.targetAright.correct) values.correct = 1 else values.correct = 0]
/branch = [if (values.showreminder == true && trial.targetAright.latency >= values.reminderrt) trial.reminder]
</trial>

<trial reminder>
/stimulusframes = [1 = reminder]
/timeout = values.reminderduration
/correctresponse = (noresponse)
/recorddata = false
</trial>

****************************************************
blocks
****************************************************

<block practice_compatibletest_sciat>
/ bgstim = (attributeAleft, attributeBright)
/ trials = [1=instructions; 
			2-11 = noreplace(attributeA, attributeA, attributeA, attributeA, attributeA,
							 attributeB, attributeB, attributeB, attributeB, attributeB)]
/ responsemode = correct
</block>

Note: presents the trials in a  20:20:30 ratio => 40 (good + target) on responsekey_left and 30 (bad) on responsekey_right
<block compatibletest_sciat>
/ bgstim = (attributeAleft, orleft, targetAleftmixed, attributeBright)
/ trials = [1-70 = noreplace(targetAleft, targetAleft, targetAleft, targetAleft, targetAleft, targetAleft, targetAleft, targetAleft, targetAleft, targetAleft, 
							 attributeA, attributeA, attributeA, attributeA, attributeA, attributeA, attributeA, attributeA, attributeA, attributeA, 
							 attributeB, attributeB, attributeB, attributeB, attributeB, attributeB, attributeB, attributeB, attributeB, attributeB, attributeB, attributeB, attributeB, attributeB, attributeB)]
/ ontrialend = [if(block.compatibletest_sciat.latency  <= 10000) values.sum1 =  values.sum1 + block.compatibletest_sciat.latency]
/ ontrialend = [if(block.compatibletest_sciat.latency  <= 10000) values.n1 =  values.n1 + 1]
/ ontrialend = [if(block.compatibletest_sciat.latency  <= 10000) values.ss1 =  values.ss1 + (block.compatibletest_sciat.latency * block.compatibletest_sciat.latency)]
/ ontrialend = [if(block.compatibletest_sciat.latency  <= 10000) values.n_correct += block.compatibletest_sciat.correct]
/ responsemode = correct
</block>

Note: presents the trials in a  7:7:10 ratio => 10 (good) on responsekey_left and 14 (bad + target) on responsekey_right
<block practice_incompatibletest_sciat>
/ bgstim = (attributeAleft, attributeBright)
/ trials = [1=instructions; 
			2-11 = noreplace(attributeA, attributeA, attributeA, attributeA, attributeA,
							 attributeB, attributeB, attributeB, attributeB, attributeB)]
/ responsemode = correct
</block>

Note: presents the trials in a  7:7:10 ratio => 10 (good) on responsekey_left and 14 (bad + target) on responsekey_right
<block incompatibletest_sciat>
/ bgstim = (attributeAleft, targetARightMixed, orright, attributeBright)
/ trials = [1-70 = noreplace(targetAright, targetAright, targetAright, targetAright, targetAright, targetAright, targetAright, targetAright, targetAright, targetAright, 
							 attributeA, attributeA, attributeA, attributeA, attributeA, attributeA, attributeA, attributeA, attributeA, attributeA, attributeA, attributeA, attributeA, attributeA, attributeA,   
							 attributeB, attributeB, attributeB, attributeB, attributeB, attributeB, attributeB, attributeB, attributeB, attributeB, )]
/ ontrialend = [if(block.incompatibletest_sciat.latency  <= 10000) values.sum2 =  values.sum2 + block.incompatibletest_sciat.latency]
/ ontrialend = [if(block.incompatibletest_sciat.latency  <= 10000) values.n2 =  values.n2 + 1]
/ ontrialend = [if(block.incompatibletest_sciat.latency  <= 10000) values.ss2 =  values.ss2 + (block.incompatibletest_sciat.latency * block.incompatibletest_sciat.latency)]
/ ontrialend = [if(block.incompatibletest_sciat.latency  <= 10000) values.n_correct += block.incompatibletest_sciat.correct]
/ responsemode = correct
</block>

<block compatibletest_sciatinstructions>
/ bgstim = (attributeAleft, orleft, targetAleftmixed, attributeBright)
/ trials = [1=instructions]
/ recorddata = false
</block>

<block incompatibletest_sciatinstructions>
/ bgstim = (attributeAleft, attributeBright, orright, targetArightmixed)
/ trials = [1=instructions]
/ recorddata = false
</block>

<block summary>
/skip = [values.showsummaryfeedback == false]
/ trials = [1=summary]
/ recorddata = false
</block>

****************************************************
experiment
****************************************************

# chinese neg
<expt>
/subjects = (1 of 8)
/groupassignment = subjectnumber
/ blocks = [1=practice_compatibletest_sciat; 2=compatibletest_sciatinstructions; 3=compatibletest_sciat;
			4=incompatibletest_sciatinstructions; 5=incompatibletest_sciat; 6=summary]
/onexptend = [values.completed = 1]
</expt>

# chinese pos
<expt>
/subjects = (2 of 8)
/groupassignment = subjectnumber
/ blocks = [1=practice_incompatibletest_sciat; 2=incompatibletest_sciatinstructions; 3=incompatibletest_sciat; 
			4=compatibletest_sciatinstructions; 5=compatibletest_sciat; 6=summary]
/onexptend = [values.completed = 1]
</expt>

# chinese neg
<expt>
/subjects = (3 of 8)
/groupassignment = subjectnumber
/ blocks = [1=practice_compatibletest_sciat; 2=compatibletest_sciatinstructions; 3=compatibletest_sciat;
			4=incompatibletest_sciatinstructions; 5=incompatibletest_sciat; 6=summary]
/onexptend = [values.completed = 1]
</expt>

# chinese pos
<expt>
/subjects = (4 of 8)
/groupassignment = subjectnumber
/ blocks = [1=practice_incompatibletest_sciat; 2=incompatibletest_sciatinstructions; 3=incompatibletest_sciat; 
			4=compatibletest_sciatinstructions; 5=compatibletest_sciat; 6=summary]
/onexptend = [values.completed = 1]
</expt>


# chinese pos
<expt>
/subjects = (5 of 8)
/groupassignment = subjectnumber
/ blocks = [1=practice_incompatibletest_sciat; 2=incompatibletest_sciatinstructions; 3=incompatibletest_sciat; 
			4=compatibletest_sciatinstructions; 5=compatibletest_sciat; 6=summary]
/onexptend = [values.completed = 1]
</expt>

# chinese neg
<expt>
/subjects = (6 of 8)
/groupassignment = subjectnumber
/ blocks = [1=practice_compatibletest_sciat; 2=compatibletest_sciatinstructions; 3=compatibletest_sciat;
			4=incompatibletest_sciatinstructions; 5=incompatibletest_sciat; 6=summary]
/onexptend = [values.completed = 1]
</expt>

# chinese pos
<expt>
/subjects = (7 of 8)
/groupassignment = subjectnumber
/ blocks = [1=practice_incompatibletest_sciat; 2=incompatibletest_sciatinstructions; 3=incompatibletest_sciat; 
			4=compatibletest_sciatinstructions; 5=compatibletest_sciat; 6=summary]
/onexptend = [values.completed = 1]
</expt>

# chinese neg
<expt>
/subjects = (8 of 8)
/groupassignment = subjectnumber
/ blocks = [1=practice_compatibletest_sciat; 2=compatibletest_sciatinstructions; 3=compatibletest_sciat;
			4=incompatibletest_sciatinstructions; 5=incompatibletest_sciat; 6=summary]
/onexptend = [values.completed = 1]
</expt>

****************************************************
Test Monkey
****************************************************

<monkey>
/ latencydistribution = normal(500, 100)
/ percentcorrect = 90
</monkey>
